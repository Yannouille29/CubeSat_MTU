import processing.serial.*;
import java.awt.event.KeyEvent;
import java.io.IOException;

Serial myPort;
String data = "";
float roll = 0, pitch = 0, yaw = 0; // Initialisation à zéro

color[] faceColors; // Tableau de couleurs pour chaque face du cube

void setup() {
  size(800, 800, P3D);
  myPort = new Serial(this, "COM3", 19200); // Démarre la communication série
  myPort.bufferUntil('\n');

  faceColors = new color[6];
  faceColors[0] = color(255, 0, 0);    // Rouge
  faceColors[1] = color(0, 255, 0);    // Vert
  faceColors[2] = color(0, 0, 255);    // Bleu
  faceColors[3] = color(255, 255, 0);  // Jaune
  faceColors[4] = color(0, 255, 255);  // Cyan
  faceColors[5] = color(255, 0, 255);  // Magenta
}

void draw() {
  background(300);
  translate(width / 2, height / 2, 0);

  // Appliquer les rotations basées sur les données de cap
  rotateX(radians(-pitch));
  rotateZ(radians(roll));
  rotateY(radians(yaw));

  // Afficher le cube avec des faces de couleurs différentes
  drawColoredCube();

  // Afficher le texte
  fill(255);
  textSize(30);
  scale(-1, 1, 1);
  text("Nimbus Cubesat", -100, 0, -126);
}

void drawColoredCube() {
  float size = 250;

  beginShape();
  fill(faceColors[0]);
  vertex(-size/2, -size/2, -size/2);
  vertex( size/2, -size/2, -size/2);
  vertex( size/2,  size/2, -size/2);
  vertex(-size/2,  size/2, -size/2);
  endShape(CLOSE);

  beginShape();
  fill(faceColors[1]);
  vertex( size/2, -size/2, -size/2);
  vertex( size/2, -size/2,  size/2);
  vertex( size/2,  size/2,  size/2);
  vertex( size/2,  size/2, -size/2);
  endShape(CLOSE);

  beginShape();
  fill(faceColors[2]);
  vertex( size/2, -size/2,  size/2);
  vertex(-size/2, -size/2,  size/2);
  vertex(-size/2,  size/2,  size/2);
  vertex( size/2,  size/2,  size/2);
  endShape(CLOSE);

  beginShape();
  fill(faceColors[3]);
  vertex(-size/2, -size/2,  size/2);
  vertex(-size/2, -size/2, -size/2);
  vertex(-size/2,  size/2, -size/2);
  vertex(-size/2,  size/2,  size/2);
  endShape(CLOSE);

  beginShape();
  fill(faceColors[4]);
  vertex(-size/2, -size/2, -size/2);
  vertex( size/2, -size/2, -size/2);
  vertex( size/2, -size/2,  size/2);
  vertex(-size/2, -size/2,  size/2);
  endShape(CLOSE);

  beginShape();
  fill(faceColors[5]);
  vertex(-size/2,  size/2, -size/2);
  vertex( size/2,  size/2, -size/2);
  vertex( size/2,  size/2,  size/2);
  vertex(-size/2,  size/2,  size/2);
  endShape(CLOSE);
}

void serialEvent(Serial myPort) {
  data = myPort.readStringUntil('\n');
  if (data != null) {
    data = trim(data);
    String items[] = split(data, '/');
    if (items.length > 1) {
      roll = float(items[0]);
      pitch = float(items[1]);
      yaw = float(items[2]);
    }
  }
}
